plugins {
    id "java"
    id "idea"
    id "org.springframework.boot" version "2.6.5"
    id "io.spring.dependency-management" version "1.0.11.RELEASE"
    id "io.freefair.lombok" version "6.4.1"
    id "com.github.spotbugs" version "5.0.6"
    id "com.github.sherter.google-java-format" version "0.9"
    id "com.github.ben-manes.versions" version "0.42.0"
    id "org.owasp.dependencycheck" version "7.0.3"
}

googleJavaFormat {
    toolVersion = "1.13.0"
}

lombok {
    version = "1.18.22"
    config["lombok.anyConstructor.addConstructorProperties"] = "true"
}

ext {
    jacksonDataBindVersion = "2.13.2.2"
    jacksonCoreVersion = "2.13.2"
    javaUuidGeneratorVersion = "4.0.1"
    logstashVersion = "7.0.1"
    caffeineVersion = "3.0.6"
    wiremockVersion = "2.27.2"
    archUnitVersion = "0.23.1"
}

repositories {
    mavenCentral()
}

bootJar {
    archiveFileName = "${archiveBaseName.get()}.${archiveExtension.get()}"
}

dependencies {
    implementation "org.springframework.boot:spring-boot-starter-web"
    implementation "org.springframework.boot:spring-boot-starter-cache"
    implementation "org.springframework.boot:spring-boot-starter-actuator"
    implementation "org.springframework.boot:spring-boot-starter-oauth2-client"
    implementation "com.fasterxml.jackson.core:jackson-core:${jacksonCoreVersion}"
    implementation "com.fasterxml.jackson.core:jackson-databind:${jacksonDataBindVersion}"
    implementation "com.fasterxml.uuid:java-uuid-generator:${javaUuidGeneratorVersion}"
    implementation "net.logstash.logback:logstash-logback-encoder:${logstashVersion}"
    implementation "com.github.ben-manes.caffeine:caffeine:${caffeineVersion}"

    testImplementation("org.springframework.boot:spring-boot-starter-test") {
        exclude group: "org.junit.vintage", module: "junit-vintage-engine"
    }
    testImplementation "com.github.tomakehurst:wiremock:${wiremockVersion}"
    testImplementation "com.tngtech.archunit:archunit-junit5:${archUnitVersion}"
}

group = "de.otto.platform"
sourceCompatibility = 11
targetCompatibility = 11

tag {
    message fromMandatorySystemProperty('message')
}

import java.text.SimpleDateFormat

bootJar {
    manifest {
        attributes(
                'Implementation-Vendor': project.group,
                'Implementation-Title': project.name,
                'Implementation-Version': project.version,
                'Built-By': System.properties['user.name'],
                'Created-By': "Gradle ${gradle.gradleVersion}",
                'Build-Timestamp': new SimpleDateFormat("yyyy-MM-dd'T'HH:mm:ss.SSSZ").format(new Date()),
                'Build-Jdk': "${System.properties['java.version']} (${System.properties['java.vendor']} ${System.properties['java.vm.version']})",
                'Build-OS': "${System.properties['os.name']} ${System.properties['os.arch']} ${System.properties['os.version']}"
        )
    }
}

apply from: "${rootDir}/gradle/test.gradle"
apply from: "${rootDir}/gradle/spotbugs.gradle"
apply from: "${rootDir}/gradle/dependencycheck.gradle"
apply from: "${rootDir}/gradle/checkstyle.gradle"
apply from: "${rootDir}/gradle/pmd.gradle"
apply from: "${rootDir}/gradle/jacoco.gradle"
